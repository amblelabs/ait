plugins {
    id "fabric-loom" version "1.9-SNAPSHOT"
    id "maven-publish"

    id "com.diffplug.spotless" version "6.20.0"
}

apply from: 'versioning.gradle'

version = getCurrentVersion()
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    maven {
        url "https://jitpack.io"

        metadataSources {
            artifact() // Look directly for artifact
        }
    }

    // Dependency for GravityAPI
    maven { url "https://maven.ladysnake.org/releases" }
    maven { url "https://maven.shedaniel.me/" }

    // YACL
    maven { url 'https://maven.isxander.dev/releases' }

    // ModMenu
    maven { url "https://maven.terraformersmc.com/releases/" }

    // K9
    maven { url "https://api.modrinth.com/maven" }

    // JEI
    maven { url "https://maven.blamejared.com/" }

    // Scheduler, Queue, MultiDim, AutoJSON
    maven { url "https://theo.is-a.dev/maven/" }
}

loom {
    accessWidenerPath = file("src/main/resources/ait.accesswidener")

    runs {
        // This adds a new gradle task that runs the datagen API
        datagen {
            inherit server
            name "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"
            vmArg "-Dfabric-api.datagen.modid=ait"

            runDir "build/datagen"
        }
    }
}

// Add the generated resources to the main source set
sourceSets {
    main {
        resources {
            srcDirs += [
                    "src/main/generated"
            ]
        }
    }
}
dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // Misc (non-MC) libraries
    include(implementation "net.objecthunter:exp4j:${project.exp4j_version}")
    implementation "com.google.code.gson:gson:${project.gson_version}"

    // MC libraries
    modImplementation "dev.isxander:yet-another-config-lib:${project.yacl_version}"

    // TODO: move these 3 to modkit so we don't have to re-export so many dependencies
    include(modApi("dev.drtheo:multidim:${project.multidim_version}")) {
        exclude(group: "net.fabricmc.fabric-api")
        transitive = true
    }
    include(modApi("dev.drtheo:scheduler:${project.scheduler_version}")) {
        exclude(group: "net.fabricmc.fabric-api")
        transitive = true
    }
    include(modApi("dev.drtheo:queue:${project.queue_version}")) {
        exclude(group: "net.fabricmc.fabric-api")
        transitive = true
    }

    modApi("com.github.amblelabs:modkit:v${project.amblekit_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    // Compat
    modImplementation("com.terraformersmc:modmenu:${project.modmenu_version}")

    modCompileOnly "maven.modrinth:k9-mod:${project.k9_version}"

    modImplementation("mezz.jei:jei-${project.minecraft_version}-fabric-api:${project.jei_version}")
    // Using Modrinth maven because JEI's official maven crashed with Yarn mappings
    modRuntimeOnly("maven.modrinth:jei:${project.jei_version}")

    modCompileOnly("com.github.qouteall:GravityChanger:${project.gravity_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    modCompileOnly "me.lucko:fabric-permissions-api:${project.fabric_permissions_api_version}"
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE // Exclude duplicate files, I don"t why this even works
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    inputs.property "amblekit_version", project.amblekit_version
    inputs.property "yacl_version", project.yacl_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version,
                "amblekit_version": project.amblekit_version,
                "yacl_version": project.yacl_version
    }
}

def targetJavaVersion = 17
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with sound characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible) {
        it.options.release = targetJavaVersion
    }
}

spotless {
    enforceCheck = false

    format 'misc', {
        // define the files to apply `misc` to
        target '*.gradle', '.gitattributes', '.gitignore'

        // define the steps to apply to those files
        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }

    java {
        //eclipse()

        removeUnusedImports()
        importOrder('java', 'javax', '', 'net.minecraft', group)

        indentWithSpaces()
        trimTrailingWhitespace()

        formatAnnotations()
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }

    archivesBaseName = project.base.archivesName.get()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }

    // exclude assets if it's a beta dev build
    if (System.hasProperty("BETA")) {
        processResources.exclude("assets")
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}

def gitBranch = 'git rev-parse --abbrev-ref HEAD'.execute().text.trim()

tasks.processResources {
    filesMatching("*.properties") {
        expand(branch: gitBranch)
    }
}
